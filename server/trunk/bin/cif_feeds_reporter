#!/usr/bin/perl

use warnings;
use strict;

# fix lib paths, some may be relative
BEGIN {
    require File::Spec;
    my @libs = ("lib", "local/lib");
    my $bin_path;

    for my $lib (@libs) {
        unless ( File::Spec->file_name_is_absolute($lib) ) {
            unless ($bin_path) {
                if ( File::Spec->file_name_is_absolute(__FILE__) ) {
                    $bin_path = ( File::Spec->splitpath(__FILE__) )[1];
                }
                else {
                    require FindBin;
                    no warnings "once";
                    $bin_path = $FindBin::Bin;
                }
            }
            $lib = File::Spec->catfile( $bin_path, File::Spec->updir, $lib );
        }
        unshift @INC, $lib;
    }

}

use Getopt::Std;
require CIF::Archive::DataType::Plugin::Feed::Search;
use Data::Dumper;
use MIME::Lite;
use DateTime;
use DateTime::Format::DateParse;
use Text::Table;

my %opts;
getopts('hLt:e:',\%opts);
my $limit   = $opts{'L'} || 100;
my $time    = $opts{'t'} || (time() - 84600);
my $admin   = $opts{'e'} || 'root';
if($time =~ /^\d+$/){
    $time = DateTime->from_epoch(epoch => $time);
} else {
    $time = DateTime::Format::DateParse->parse_datetime($time) || die('time format: '.$time.' not supported');
}
$time = $time->ymd().'T'.$time->hms().'Z';
my $reporttime = DateTime->from_epoch(epoch => time());

my $p = 'CIF::Archive::DataType::Plugin::Feed::Search';
my @recs = $p->search_feed($time,$limit);

my @cols = ('count','description','severity','confidence','restriction');
my $table = Text::Table->new(@cols);
foreach my $r (@recs){
    $r->{'description'} =~ s/search //;
    $table->load([ map { $r->{$_} } @cols ]);
}

my $report = <<"EOF";
Report Window: $time - $reporttime

$table

EOF

my $msg = MIME::Lite->new(
    To      => $admin,
    Subject => 'CIF Feed Report',
    Data    => $report,
)->send() || die($!);
