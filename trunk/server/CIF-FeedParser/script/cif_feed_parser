#!/usr/bin/perl -w

use strict;
use Getopt::Std;
use CIF::FeedParser;
use Config::Simple;
use Data::Dumper;
use Net::DNS::Resolver;

my %opts;
getopts('hs:T:t:dFc:f:',\%opts);
my $debug = $opts{'d'};
my $config = $opts{'c'} || $ENV{'HOME'}.'/.cif';
my $server_config = $opts{'s'} || '/etc/cif/server.ini';
my $throttle = $opts{'T'} || 'low';
my $c = Config::Simple->new($config) || die($!.' '.$config);
my $sconfig = Config::Simple->new($server_config) if(-e $server_config);
my $f = $opts{'f'};
die usage() if($opts{'h'} || !$f);
my $default = $c->param(-block => 'default');
my $hash = $c->param(-block => $f);
my $full_load = $opts{'F'};

sub usage {
    return <<EOF;
Usage: perl $0 -c /opt/cif-feeds-public/etc/cif/misc.cfg -f malwaredomainlist -T low
    -h  --help:     this message
    -f  --feed:     feed name (eg: section header in the configuration file)
    -c  --config:   specify the configuration (or 'rules') file (default: $config)
    -s  --sconfig:  server config location (database stuff, default: $server_config)
    -F  --full:     "full load", meaning don't do NS resolution on domains
    -d --debug:     debug
    -T  --throttle: throttle (how man threads to use, default: $throttle)
                    low:    threads = 1/2 number of cores
                    medium: threads = number of cores
                    high:   threads = 2x number of cores

Examples:
    \$ cif_feed_parser -c /opt/cif-feeds-public/etc/cif/spyeyetracker.cfg -f binaries -T medium -F
    \$ cif_feed_parser -c /opt/cif-feeds-public/etc/cif/misc.cfg -f malwaredomainlist -T high -F -d
EOF
}

unless(keys %$hash){
    die('section doesn\'t exist: '.$f."\n\n".usage());
}

foreach my $h (keys %$hash){
    $default->{$h} = $hash->{$h};
}

$c = $default;
my $threaded = $opts{'t'} || $c->{'threads_count'} || CIF::FeedParser::throttle($throttle);
$c->{'threads_count'} = $threaded if($threaded);

unless($full_load){
    if(my $val = $hash->{'no_resolve'}){
        $full_load = 1 if($val eq '1' || $val eq 'true');        
    }
}

if($debug){
    warn 'running '.$c->{'threads_count'}.' threads';
}

my $nsres;
unless($full_load){
    $c->{nsres} = Net::DNS::Resolver->new(recursive => 0);
}

if($sconfig && $sconfig->param(-block => 'database')){
    $sconfig = $sconfig->param(-block => 'database');
    $c->{'database'} = ['DBI:'.$sconfig->{'driver'}.':database='.$sconfig->{'database'}.';host='.$sconfig->{'host'},$sconfig->{'user'},$sconfig->{'password'},{AutoCommit => 1}];
}

my @items = CIF::FeedParser::parse($c);
exit(0) unless($#items > -1);
CIF::FeedParser::t_insert($full_load,undef,@items);
exit(0);
